plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'org.openjfx.javafxplugin' version '0.0.14'
    id 'org.gradlex.extra-java-module-info' version '1.4.1'
}

group 'jtorrent'
version '1.0-SNAPSHOT'

sourceCompatibility = '17'
targetCompatibility = '17'

application {
    mainModule = 'jtorrent'
    mainClass = 'jtorrent.Main'
    // Workaround for the following error:
    // java.lang.IllegalAccessError: class ch.qos.logback.core.boolex.JaninoEventEvaluatorBase
    // (in module ch.qos.logback.core) cannot access class org.codehaus.janino.ScriptEvaluator
    // (in module org.codehaus.janino) because module ch.qos.logback.core does not read module org.codehaus.janino
    applicationDefaultJvmArgs = ['--add-reads', 'ch.qos.logback.core=org.codehaus.janino']
}

repositories {
    mavenCentral()
}

checkstyle {
    toolVersion = '10.3.4'
}

javafx {
    version = '11'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation 'com.dampcake:bencode:1.4'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.6'
    implementation 'io.github.mkpaz:atlantafx-base:2.0.1'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-materialdesign2-pack:12.3.1'
    implementation 'ch.qos.logback:logback-classic:1.5.6'
    implementation 'org.codehaus.janino:janino:3.1.12'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

extraJavaModuleInfo {
    module('com.dampcake:bencode', 'com.dampcake.bencode', '1.4') {
        exports('com.dampcake.bencode')
    }
}

test {
    useJUnitPlatform()
}